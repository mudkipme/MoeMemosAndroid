plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.google.dagger.hilt.android'
    id 'com.google.devtools.ksp'
    id 'org.jetbrains.kotlin.plugin.compose'
}

def releaseSigningStoreFile = System.getenv("ANDROID_SIGNING_STORE_FILE")
def releaseSigningStorePassword = System.getenv("ANDROID_SIGNING_STORE_PASSWORD")
def releaseSigningKeyAlias = System.getenv("ANDROID_SIGNING_KEY_ALIAS")
def releaseSigningKeyPassword = System.getenv("ANDROID_SIGNING_KEY_PASSWORD")
def hasReleaseSigning = [
    releaseSigningStoreFile,
    releaseSigningStorePassword,
    releaseSigningKeyAlias,
    releaseSigningKeyPassword
].every { it != null && !it.trim().isEmpty() }

android {
    compileSdk = 36

    defaultConfig {
        applicationId = "me.mudkip.moememos"
        minSdk = 26
        targetSdk = 36
        versionCode = 41
        versionName = "0.9.0-alpha.1"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary = true
        }
    }

    signingConfigs {
        release {
            if (hasReleaseSigning) {
                storeFile file(releaseSigningStoreFile)
                storePassword releaseSigningStorePassword
                keyAlias releaseSigningKeyAlias
                keyPassword releaseSigningKeyPassword
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            crunchPngs = false
            if (hasReleaseSigning) {
                signingConfig signingConfigs.release
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        compose = true
    }
    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    namespace = 'me.mudkip.moememos'
}

dependencies {
    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_material"
    implementation "androidx.compose.material3:material3:$material3"
    implementation "androidx.compose.material3.adaptive:adaptive:$material3_adaptive"
    implementation "androidx.compose.material3.adaptive:adaptive-layout:$material3_adaptive"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycle_version"
    implementation "androidx.activity:activity-compose:$activity_compose_version"
    implementation "androidx.navigation:navigation-compose:$nav_version"
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    implementation "androidx.paging:paging-compose:$paging_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-kotlinx-serialization:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:$okhttp_version"
    implementation "com.github.skydoves:sandwich:$sandwich"
    implementation "com.github.skydoves:sandwich-retrofit:$sandwich"
    implementation "com.github.skydoves:sandwich-retrofit-serialization:$sandwich"
    implementation "io.coil-kt.coil3:coil-compose:$coil_version"
    implementation "io.coil-kt.coil3:coil-network-okhttp:$coil_version"
    implementation "org.jetbrains:markdown:$markdown_version"
    implementation "com.mikepenz:multiplatform-markdown-renderer-m3:$markdown_renderer_version"
    implementation "com.mikepenz:multiplatform-markdown-renderer-code:$markdown_renderer_version"
    implementation "com.mikepenz:multiplatform-markdown-renderer-coil3:$markdown_renderer_version"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    ksp "com.google.dagger:hilt-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-navigation-compose:$hilt_navigation_compose_version"

    implementation "androidx.datastore:datastore:$datastore"
    implementation "androidx.datastore:datastore-preferences:$datastore"
    implementation "androidx.datastore:datastore-preferences-core:$datastore"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.10.0"
    implementation "com.jakewharton.timber:timber:5.0.1"
    implementation "net.swiftzer.semver:semver:$semver_version"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
    
    // Glance for App Widget
    implementation "androidx.glance:glance:$glance_version"
    implementation "androidx.glance:glance-appwidget:$glance_version"
    implementation "androidx.glance:glance-material3:$glance_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"
}

ksp {
    arg("room.schemaLocation", "$projectDir/schemas")
}
